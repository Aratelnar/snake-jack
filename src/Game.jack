class Game {
    field Field _field;
    field int direction; // right, down, left, up

    static Vector down;
    static Vector right;
    static Vector up;
    static Vector left;

    constructor Game new(Field f){
        let _field = f;
        let down = Vector.new(0, 1);
        let right = Vector.new(1, 0);
        let up = Vector.new(0, -1);
        let left = Vector.new(-1, 0);
        let direction = right;
        return this;
    }

    method void run(){
        var char key;
        var boolean exit;
        var int tick;
        let exit = false;
        let tick = 1;

        do _field.init();

        do Output.printString("Score: ");
        while (~(exit)){
            let key = Keyboard.keyPressed();

            if (key = 140){
                let exit = true;
            }

            if (key = 132){
                do changeDirection(0); // right
            }
            if (key = 133){
                do changeDirection(1); // down
            }
            if (key = 130){
                do changeDirection(2); // left
            }
            if (key = 131){
                do changeDirection(3); // up
            }

            if (tick = 0){
                let exit = _field.handleDirection(getVector(direction));
            }

            let tick = Math2.mod(tick + 1, 10);

            do Sys.wait(20);
        }

        do Output.moveCursor(10,27);
        do Output.printString("           ");
        do Output.moveCursor(11,27);
        do Output.printString(" Game Over ");
        do Output.moveCursor(12,27);
        do Output.printString("           ");
        return;
    }

    method void changeDirection(int dir){
        if (direction = (dir + 2)){
            return;
        }
        if (direction = (dir - 2)){
            return;
        }
        let direction = dir;
        return;
    }

    method Vector getVector(int dir){
        if (dir = 0){
            return right;
        }
        if (dir = 1){
            return down;
        }
        if (dir = 2){
            return left;
        }
        if (dir = 3){
            return up;
        }
        return right;
    }

    method void dispose(){
        do _field.dispose();
        do right.dispose();
        do up.dispose();
        do left.dispose();
        do down.dispose();
        do Memory.deAlloc(this);
        return;
    }
}
